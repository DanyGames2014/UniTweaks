//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyUnusedAssignment
//file:noinspection ForeignDelegate
plugins {
    id 'maven-publish'
    id 'fabric-loom' version '1.10.1'
    id 'babric-loom-extension' version '1.10.1'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/unitweaks.accesswidener")
}

repositories {
    // Used for Babric Loom Extensions
    maven {
        name = 'Babric'
        url = 'https://maven.glass-launcher.net/babric'
    }

    // Used for many dependencies
    maven {
        name = 'Glass Releases'
        url = 'https://maven.glass-launcher.net/releases'
    }

    // Used for Snapshots
    maven {
        name = 'Glass Snapshots'
        url = 'https://maven.glass-launcher.net/snapshots'
    }

    // NyaRepo
    maven {
        name = "nyarepo"
        url = "https://maven.fildand.cz/releases"
    }

    // Used for a StationAPI dependency.
    maven {
        name = 'Froge'
        url 'https://maven.minecraftforge.net/'
    }

    // Used for projects that do not have a maven repository, but do have a GitHub repository with working build scripts.
    maven {
        name = 'Jitpack'
        url = 'https://jitpack.io'
    }

    // Modrinth Maven
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }

    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.glasslauncher:biny:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    implementation "org.slf4j:slf4j-api:1.8.0-beta4"
    implementation 'org.apache.logging.log4j:log4j-slf4j18-impl:2.17.2'
    implementation "me.carleslc:Simple-Yaml:1.8.4"

    modImplementation "net.modificationstation:StationAPI:${project.stapi_version}"

    modImplementation("net.glasslauncher.mods:ModMenu:${project.modmenu_version}") {
        transitive false
    }
    
    modImplementation("net.glasslauncher.mods:glass-networking:${project.glass_networking_version}") {
        transitive false
    }

    modImplementation("net.glasslauncher.mods:GlassConfigAPI:${project.gcapi_version}") {
        transitive false
    }

    modRuntimeOnly("net.glasslauncher.mods:AlwaysMoreItems:${project.alwaysmoreitems_version}") {
        transitive false
    }

    modRuntimeOnly("com.github.matthewperiut:retrocommands:${project.retrocommands_version}") {
        transitive false
    }

    modRuntimeOnly("net.danygames2014:spawneggs:${project.spawneggs_version}") {
        transitive false
    }

    modRuntimeOnly("maven.modrinth:retroauth:${project.retroauth_version}-fabric,b1.7.3") {
        transitive false
    }
    
    modRuntimeOnly("maven.modrinth:fast-stapi-intro:2.0.0-fabric,b1.7.3") {
        transitive false
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    repositories {
        mavenLocal()
        if(project.hasProperty("nyarepo_publishing_cave_username")){
            maven {
                url = "https://maven.fildand.cz/releases"
                credentials {
                    username "${project.nyarepo_publishing_cave_username}"
                    password "${project.nyarepo_publishing_cave_password}"
                }
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
